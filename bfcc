#!/usr/bin/perl

use warnings;
use strict;
use 5.010;
use File::Basename;
use File::Slurp qw<read_file write_file>;

my %subs = (
    ',' => '*h=getchar();',   # Read a character from stdin, 255 on EOF
    '.' => 'putchar(*h);',    # Write a character to stdout
    '-' => '--*h;',           # Decrement tape
    '+' => '++*h;',           # Increment tape
    '<' => '--h;',            # Move head left
    '>' => '++h;',            # Move head right
    '[' => 'while(*h) {',     # Start loop
    ']' => '}',               # End loop

    # Fixed optimisations:
    '[-]'    => '*h = 0;',
    '[->+<]' => 'h[1] += h[0]; *h = 0;',
);
$_ .= "\n" for values %subs;

my $header = <<END_HEADER;
#include <stdlib.h>

int main() {
    unsigned char* h;
    h = calloc(10000, 1);
END_HEADER

my $footer = <<END_FOOTER;
    return 0;
}
END_FOOTER

my $ignorable = do {
    my $chars = join '', map { "\\$_" } sort grep { length == 1 } keys %subs;
    qr/[^$chars]+/;
};

my $token = do {
    my $alternations = join '|', map { quotemeta }
        # Ensure longer alternations are tried first
        sort { length $b <=> length $a || $a cmp $b } keys %subs;
    qr/($alternations)/;
};

foreach my $bf_file (@ARGV) {
    my $code = read_file($bf_file);
    $code =~ s/$ignorable//g;
    $code =~ s/$token/$subs{$1}/g;

    my $cfile = basename($bf_file, ".bf").".c";
    write_file($cfile, $header, $code, $footer);

    system("gcc", $cfile) == 0 or exit 1;
}
