#!/usr/bin/perl

use warnings;
use strict;
use 5.010;
use File::Basename;

my %subs = (
    ',' => '*h=getchar();',   # Read a character from stdin, 255 on EOF
    '.' => 'putchar(*h);',    # Write a character to stdout
    '-' => '--*h;',           # Decrement tape
    '+' => '++*h;',           # Increment tape
    '<' => '--h;',            # Move head left
    '>' => '++h;',            # Move head right
    '[' => 'while(*h) {',     # Start loop
    ']' => '}',               # End loop
);
$_ .= "\n" for values %subs;

my $header = <<END_HEADER;
#include <stdlib.h>

int main() {
    unsigned char* h;
    h = calloc(10000, 1);
END_HEADER

my $footer = <<END_FOOTER;
    return 0;
}
END_FOOTER

foreach my $bf_file (@ARGV) {
    open my $BF, "<", $bf_file or die "Can't open $bf_file for reading: $!\n";
    my $cfile = basename($bf_file, ".bf").".c";
    open my $C, ">", $cfile or die "Can't open $cfile for writing: $!\n";
    print $C $header;
    while (<$BF>) {
        no warnings qw<uninitialized>;
        s/(.)/$subs{$1}/xmsg;
        print $C $_;
    }
    print $C $footer;
    close $BF or die "Failed to read $bf_file: $!\n";
    close $C or die "Failed to write $cfile: $!\n";
    system("gcc", $cfile) == 0 or exit 1;
}
